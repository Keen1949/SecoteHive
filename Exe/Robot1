'DOUT(1)  机械手取料OK
'DOUT(2)  
'DOUT(3) 组装OK
'DOUT(5) 开真空
'DIN(1) Tray取料OK
'DIN(2)  
'DIN(3) 托盘拍照OK  
'DIN(4)   
'DIN(5) 真空检测  
'DIN(6)   
'DIN(7)   


'机械手点位说明
'P0   回原点
'P10  到抓料位置附近
'P11  到抓料位抓料
'P12  离开抓料位
'P20  移动到第一个过渡点
'P21 移动到第二个过渡点
'P22 移动到放料位置上方
'P23  移动到放料位
'P30 从托盘抬起机械手
'P31  回到初始点等待物料

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
GLOBAL
X=0.0
Y=0.0
A=0.0
SP=10
N=0
END


PROGRAM MAIN
TOOL=TOOL1
PRINT IP1,"BUFFRESET"

INPUT IP1,N,X,Y,A
IF N==1 THEN backhome
HHHH:
INPUT IP1,N,X,Y,A
if N==8 THEN calib '自动标定
if N==2 THEN GOTO LOOP ELSE GOTO HHHH
LOOP:
ACCEL=100
DECEL=100
jump_catch
jump_p23
LABWAIT:
INPUT IP1,N,X,Y,A
if N==7 THEN GOTO ADJUST 
IF N==6 THEN GOTO PUTFPC
IF N==11 THEN GOTO DROPFPC 

GOTO LABWAIT
ADJUST:
robot_put_adjust 
GOTO LOOP
PUTFPC:
robot_fpc
GOTO LOOP
DROPFPC:
robot_drop 
GOTO LOOP

'IF N==2 THEN robot_catch_2
'IF N==3 THEN catch_away
'IF N==12 THEN jump_photo
'IF N==4 THEN photo_position_2
'IF N==5 THEN adjust_photo
'IF N==6 THEN put_product
'IF N==7 THEN adjust_angle_photo
'IF N==8 THEN VAC_check
'IF N==9 THEN robot_put_catch
'IF N==10 THEN backup_1
'IF N==11 THEN backup_2
'GOTO LOOP
END

PROGRAM calib
ACCEL=50
DECEL=50
jump_catch
jump_p23
PRINT IP1,"calib/READY"
CCCC:
INPUT IP1,N,X,Y,A
if N==18 THEN toolCalib ELSE GOTO CCCC
PRINT IP1,"calib/OK"
RETURN
END

'工具坐标的标定
PROGRAM toolCalib
ACCUR=FINE
DISABLE PASS
FOR K=0 TO 8
 H = K / 3
 MOVE P23 +POINT(3*(K MOD 3)-6,3*H-6,0,0)
 PRINT IP1,HERE.X,HERE.Y,HERE.C
 INPUT IP1,N,X,Y,A
 IF N==81 THEN K=K-1
NEXT K
U1:
MOVE HERE +POINT(0,0,0,-30)
PRINT IP1,HERE.X,HERE.Y,HERE.C
INPUT IP1,N,X,Y,A
IF N==81 THEN GOTO U1
U2:
MOVE HERE +POINT(0,0,0,60)
PRINT IP1,HERE.X,HERE.Y,HERE.C
INPUT IP1,N,X,Y
IF N==81 THEN GOTO U2 
RETURN
END

PROGRAM backhome
SPEED=10
'DISABLE NOWAIT
'ACCUR=COARSE
'SAFE=HERE
'IF SAFE.Y>=120 AND SAFE.Y<200 THEN GOTO L1
'IF SAFE.Y>=-20 AND SAFE.Y<120 THEN GOTO L0
'IF SAFE.Y>=-300 AND SAFE.Y<-20 THEN GOTO L2
IF X>0 AND X<=10 THEN SP = X  '初始化时设定机器人速度百分比
DOUT(-1)'关闭机械手取料OK
DOUT(-5)'关真空
DOUT(-3)'关组装OK
MOVEA 3,146
MOVE P0
GOTO L8

L0:
MOVE P0
GOTO L8

L1:
MOVE SAFE+POINT(0,-20,0,0)  'Y后退20mm
MOVE N11
GOTO L0

L2:
MOVE SAFE+POINT(0,0,20,0)   'Z抬升20mm
GOTO L0

L8:
PRINT IP1,"backhome/OK"
RETURN
END


PROGRAM jump_catch
ACCUR=COARSE
PASS=60
ENABLE PASS
SPEED=10*SP
MOVE P11+POINT(0,0,6,0)  WITH PASS=95
DOUT(-3)
IF DIN(1) THEN GOTO PP11 ELSE GOTO PP11UP  
PP11UP:
DISABLE PASS
WAIT DIN(1)   '等待Tray取料OK信号
DOUT(5)
MOVE P11
GOTO PP11END

PP11:
DOUT(5)
DISABLE PASS
MOVE P11

PP11END:
 PRINT IP1,"robot_catch/OK" '告诉tray盘站可以关真空
RETURN
END


PROGRAM jump_p23

ACCUR=COARSE '大致定位
WAIT DIN(5)  '真空检测  
IF DIN(3) THEN GOTO PP23 ELSE GOTO PP21 '等待托盘拍照OK
PP23:
ACCUR=COARSE
PASS=60
ENABLE PASS '开启快捷动作
SPEED=10*SP
MOVE P11+POINT(0,0,6,0) WITH PASS=95 '从抓料位置上抬
DOUT(1) '打开机器人取料OK信号
MOVE P23+POINT(0,0,8,0)'移动到放料位上方
ACCUR=FINE '精确定位
DISABLE PASS
DECEL=80
MOVE P23 '移动到放料位
DOUT(-1)
'WAIT MOTION >= 100
PRINT IP1,"robot_jump_photo/OK"

GOTO PPE
PP21:
ACCUR=COARSE
PASS=60
ENABLE PASS
SPEED=10*SP
MOVE P11+POINT(0,0,6,0) WITH PASS=95
DOUT(1)
MOVE P21 '移动到放料的过渡点
DOUT(-1)
WAIT DIN(3)   '托盘拍照OK  
MOVE P23+POINT(0,0,8,0) with pass = 60
DISABLE PASS
ACCUR=FINE
DECEL=80
MOVE P23
'WAIT MOTION >= 100
PRINT IP1,"robot_jump_photo/OK"
PPE:
RETURN
END


PROGRAM robot_put_adjust
DECEL=80
ACCUR=FINE
SPEED=9*SP
if ABS(X) < 20 AND ABS(Y)<20 AND ABS(A)<45 THEN MOVE P23+POINT(X,Y,0,A)
DOUT(-5) '关真空
if ABS(X) < 20 AND ABS(Y)<20 AND ABS(A)<45 THEN  MOVE P23+POINT(X,Y,-3,A)
DELAY 0.05
ENABLE PASS
PASS=80
SPEED=10*SP
DECEL=100
MOVE P23+POINT(X,Y+5,12,A) 
DOUT(3) '组装ok
RETURN
END


PROGRAM robot_fpc
ACCUR=FINE

SPEED=9*SP
DECEL=80
DOUT(-5)
if ABS(X) < 20 AND ABS(Y)<20 AND ABS(A)<45 THEN  MOVE P23+POINT(X,Y,-3,A)
DELAY 0.05
ENABLE PASS
PASS=80
SPEED=10*SP
DECEL=100
if ABS(X) < 20 AND ABS(Y)<20 AND ABS(A)<45 THEN MOVE P23+POINT(X,Y+5,12,A) ELSE MOVE P23+POINT(0,5,12,0) 
DOUT(3)
RETURN
END

PROGRAM  robot_drop
ENABLE PASS
PASS=80
SPEED=10*SP
DECEL=100
MOVE P23+POINT(0,5,12,0) 
DOUT(3)   '屏蔽这句,机器人继续抓料组装
MOVE P31 '回到初始点等待取料
DOUT(-5)  '关闭机器人真空

RETURN
END
